
%%====================================================================
%% Task definitions
%%====================================================================

deftask gunzip( out( File ) : gz( File ) ) in bash *{
  out=unzipped_${gz%.gz}
  gzip -c -d $gz > $out
}*

deftask hmmbuild( hmm( File ) : sto( File ) ) in bash *{
  hmm=${sto%.sto}.hmm
  hmmbuild $hmm $sto
}*

deftask hmmsearch( hits( File ) : hmm( File ) fa( File ) ) in bash *{
  hits=hits_${fa%.fa}.sto
  hmmsearch -A $hits $hmm $fa
}*

deftask sto2fa( fa( File ) : sto( File ) ) in bash *{
  fa=${sto%.sto}.fa
  esl-reformat fasta $sto > $fa
}*

deftask cat( out( File ) : <lst( File )> ) in bash *{
  out=out.txt
  cat ${lst[@]} > $out
}*

deftask muscle( msa( File ) : fa( File ) ) in bash *{
  msa=msa.fa
  muscle -quiet -in $fa > $msa
}*

deftask clustalw_tree( ph( File ) : fa( File ) ) in bash *{
  ph=${fa%.fa}.ph
  clustalw -tree -infile=$fa
}*

deftask phylip_drawtree( pdf( File ) : ph( File ) )in bash *{
  pdf=tree.pdf
  ln -sf $ph intree
  echo Y | phylip drawtree
  ps2pdf plotfile $pdf
}*


%%====================================================================
%% Input data
%%====================================================================

% CHASE seed alignment
chase_seed_sto = "seed/PF03924_seed.txt";

% Chlamydomonas reinhardtii
fa_gz = "fa/Chlre4_all_proteins.fasta.gz"
        "fa/Poptr1_1_GeneModels_AllModels_20081112_aa.fasta.gz"
        "fa/proteins.Phypa1_1.AllModels.fasta.gz"
        "fa/Volca1.GeneCatalog_2007_09_13.proteins.fasta.gz"
        "fa/Selmo1_GeneModels_FilteredModels2_aa.fasta.gz"
        "fa/Arabidopsis_thaliana.TAIR10.31.pep.all.fa.gz"
        "fa/Oryza_sativa.IRGSP-1.0.31.pep.all.fa.gz";


%%====================================================================
%% Workflow definition
%%====================================================================

hmm     = hmmbuild( sto: chase_seed_sto );
fa      = gunzip( gz: fa_gz );
hits_fa = cat( lst: sto2fa( sto: hmmsearch( hmm: hmm, fa: fa ) ) );
msa     = muscle( fa: hits_fa );
tree    = clustalw_tree( fa: msa );
pdf     = phylip_drawtree( ph: tree );

%%====================================================================
%% Query
%%====================================================================

pdf;